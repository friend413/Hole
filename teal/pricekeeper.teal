#pragma version 5
// ================================================================================================
// PriceKeeper Approval Program 
// ================================================================================================
//
// App-globals:
// sym      : byte[] Symbol to keep price for   
// vaddr    : byte[] Validator account          
// nonce    : uint64 last sequence ID
// price    : uint64 current price 
// stdev    : uint64 current confidence (standard deviation)
// slot     : uint64 slot of this onchain publication
// exp      : uint64 exponent (fixed point position)
// ts       : uint64 timestamp
//
// Slots:
// 0        Input message block 
// 1        SHA256-Hashed message 
// 
// The Message format must have the packed fields:
// 
//  Field size
//  9           header      Literal "PRICEDATA"
//  1           version     int8 (Must be 1)
//  8           dest        This appId 
//  8           nonce       uint64 Sequence identifier 
//  16          symbol      String filled with spaces e.g ("ALGO/USD        ")
//  8           price       Price. 64bit integer.
//  8           priceexp    Price exponent (fixed point position). 
//  8           conf        Confidence (stdev). 64bit integer. 
//  8           slot        Valid-slot of this aggregate price.
//  8           ts          timestamp of this price submitted by PriceFetcher service
//  32          s           Signature s-component
//  32          r           Signature r-component 
//
//  Size: 146 bytes. 
//
// ------------------------------------------------------------------------------------------------


// Application creation.
int 0
txn ApplicationID
==
bnz handle_create

// Handle app call: send price message
txn OnCompletion
int NoOp
==
bnz handle_call

// Handle deletion.
txn OnCompletion
int DeleteApplication
==
bnz success

// Fail otherwise
err

handle_create:
// -----------------------------------------------------
// Handle creation 
// Arg 0: Validator address 
// Arg 1: Symbol to keep price data 
// -----------------------------------------------------

byte "vaddr"
txn ApplicationArgs 0
app_global_put

byte "sym"
txn ApplicationArgs 1
dup
len
int 16
==
assert
app_global_put

byte "nonce"
int 0
app_global_put

b success

// -----------------------------------------------------
// Handle app call
// -----------------------------------------------------

handle_call:
// Group size must be 4 to raise computational allowance to 2800

global GroupSize
int 4
==
assert

// if this is one of dummy transactions(0 or 1), exit with success
txn GroupIndex
int 3
!=
bnz success

// Verify if sender is the data validator
txn Sender 
byte "vaddr"
app_global_get
==
assert

// Retrieve message, store in slot 0
txn ApplicationArgs 0
store 0

// ------------------------------------------------------
// Validate message 
// ------------------------------------------------------

// Check length

load 0
len
int 146
==
assert

// Check header

byte "PRICEDATA"
load 0
extract 0 9
==
assert

// Check version - must be 1.

load 0
extract 9 1
byte 0x01
==
assert

// Check destination - must be this appId.

load 0
extract 10 8
btoi
txn ApplicationID
==
assert 

// Check nonce 

load 0
extract 18 8
btoi
byte "nonce"
app_global_get
>
assert

// Reject zero price 
load 0
extract 42 8
btoi
int 0
!=
assert

// Reject zero slot
load 0
extract 66 8
btoi
int 0 
!=
assert 

// Reject out-of-range exponent
load 0
extract 50 8
btoi 
int 18
<=
assert

// Check timestamp  order, must be +/-10 secs from last block ts
// (TODO: check this again)
load 0 
extract 74 8
btoi 
dup
global LatestTimestamp
int 10
-
<
//assert 
global LatestTimestamp
int 10
+
swap
>
//assert


// ed25519verify args in stack:

// data (hash of message)
load 0
extract 0 82
sha512_256

// (B) signature 
load 0
extract 82 64

// validator-address
byte "vaddr"
app_global_get

// Verify signature
ed25519verify
int 1
==
//assert

// ----------------------------------------------------------------------------
// Verified. Store data to app globals.
// ----------------------------------------------------------------------------

byte "nonce"
load 0
extract 18 8
btoi 
app_global_put

byte "ts"
load 0 
extract 74 8
btoi 
app_global_put

byte "price"
load 0
extract 42 8 
btoi
app_global_put

byte "exp"
load 0
extract 50 8 
btoi
app_global_put

byte "stdev"
load 0
extract 58 8
btoi
app_global_put

byte "slot"
load 0
extract 66 8
btoi
app_global_put

b success

// ----------------------------------------------------------------------------

fail: 
int 0
return 

success:
int 1 
return



